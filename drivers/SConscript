from building import *

cwd     = GetCurrentDir()
src     = Split('''
    drv_uart.c
    drv_por.c
    drv_gpio.c
    drv_pinmux.c
''')
CPPDEFINES = []

CPPPATH = [cwd]

CHIP_TYPE = 'cv181x'
if GetDepend('BOARD_TYPE_MILKV_DUO256M') or GetDepend('BOARD_TYPE_MILKV_DUO256M_SPINOR') or GetDepend('BOARD_TYPE_MILKV_DUO256M_SPINAND'):
    CHIP_TYPE = 'cv181x'
elif GetDepend('BOARD_TYPE_MILKV_DUO') or GetDepend('BOARD_TYPE_MILKV_DUO_SPINOR') or GetDepend('BOARD_TYPE_MILKV_DUO_SPINAND'):
    CHIP_TYPE = 'cv180x'

CPPPATH += [cwd + r'/libraries']
CPPPATH += [cwd + r'/libraries/' + CHIP_TYPE]

if GetDepend('BSP_USING_ETH'):
    src += Split('''
        drv_eth.c
        libraries/eth/dw_eth_mac.c
        libraries/eth/cvi_eth_phy.c
        libraries/eth/eth_phy_cvitek.c
    ''')
    CPPPATH += [cwd + r'/libraries/eth']


group = DefineGroup('drivers', src, depend = [''], CPPDEFINES = CPPDEFINES, CPPPATH = CPPPATH)

objs = [group]

list = os.listdir(cwd)

for item in list:
    if os.path.isfile(os.path.join(cwd, item, 'SConscript')):
        objs = objs + SConscript(os.path.join(item, 'SConscript'))

Return('objs')
